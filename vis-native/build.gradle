buildscript {
    ext {
        kotlinVersion = "1.2.10"
        protobufGradlePlugin = "0.8.3"
        protobufVersion = "3.2.0"
        ktxVersion = "1.9.7-b1"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobufGradlePlugin"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.2.1"
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = "visualisation-native"
        gdxVersion = '1.9.6'
        roboVMVersion = '2.3.1'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "kotlin"


    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}

project(":core") {
    apply plugin: "kotlin"
    apply plugin: "com.google.protobuf"

    idea.module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        downloadJavadoc = true
        downloadSources = true

    }

    task copyProto {
        copy {
            from "$projectDir/../../monitor-agent/src/main/proto"
            into "$buildDir/generated/source/proto"
        }
    }

    sourceSets.main.proto {
        srcDir "$buildDir/generated/source/proto"
    }

    compileKotlin {
        dependsOn(":core:copyProto")
        dependsOn(":core:generateProto")
    }

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        compile group: 'io.github.libktx', name: 'ktx-box2d', version: ktxVersion
        compile group: 'io.github.libktx', name: 'ktx-scene2d', version: ktxVersion
        compile group: 'io.github.libktx', name: 'ktx-collections', version: ktxVersion
        compile group: 'io.github.libktx', name: 'ktx-app', version: ktxVersion

        compile "com.google.protobuf:protobuf-java:$protobufVersion"
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.1.0"
        }

//        generatedDir "$buildDir/generated/source/proto"
    }
}

tasks.eclipse.doLast {
    delete ".project"
}