buildscript {
    ext {
        kotlin_version = '1.2.10'
        protobufGradlePlugin = "0.8.3"
        protobufVersion = "3.2.0"
        mainClass = "ms.domwillia.jvmemory.preprocessor.Preprocessor"
    }

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobufGradlePlugin"
    }
}

group 'ms.domwillia.jvmemory'
version '0.1'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile "com.google.protobuf:protobuf-java:$protobufVersion"
}

protobuf.protoc {
    artifact = "com.google.protobuf:protoc:3.1.0"
}

sourceSets.main {
    proto {
        srcDir "../protobufs/vis"
        srcDir "../protobufs/monitor"
    }

    java {
        srcDirs += ["${protobuf.generatedFilesBaseDir}/main/java"]
    }

    kotlin {
        srcDirs += ['src/main/kotlin']
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn("generateProto")
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task run(type: JavaExec) {
    dependsOn(":build")
    main = "$mainClass"
    classpath = sourceSets.main.runtimeClasspath
}